/*
    Work-in-progress checker for malloc/free usage
 */
sm malloc_checker {
  state decl any_pointer ptr;

  ptr.all:
    { ptr = malloc() } =>  ptr.unknown;

  ptr.all:
    { ptr = 0 } =>  ptr.null;

  ptr.unknown, ptr.null, ptr.nonnull:
      { ptr == 0 } => true=ptr.null, false=ptr.nonnull
    | { ptr != 0 } => true=ptr.nonnull, false=ptr.null
    ;

  ptr.unknown:
    { *ptr } => {{ error('use of possibly-NULL pointer %s' % ptr) }}, ptr.nonnull;

  ptr.null:
    { *ptr } => {{ error('use of NULL pointer %s' % ptr) }};

  ptr.all, ptr.unknown, ptr.null, ptr.nonnull:
    { free(ptr) } => ptr.free;

  ptr.free:
      { free(ptr) } => {{ error('double-free of %s' % ptr) }}
    | { ptr } => {{ error('use-after-free of %s' % ptr) }}
    ;

  ptr.unknown:
      { memset(ptr) } => {{ error('use of possibly-NULL pointer %s' % ptr) }};

  ptr.null:
      { memset(ptr) } => {{ error('use of NULL pointer %s' % ptr) }};

  ptr.free:
      { memset(ptr) } => {{ error('use-after-free of %s' % ptr) }};

  ptr.unknown, ptr.nonnull:
      $leaked$ => {{ error('leak of %s' % ptr) }};

}
